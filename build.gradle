buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'signing'

version = "1.16.5-2.0.7"

def fileVersion = "2.0.7"

group = 'de.pt400c.noitemdespawn'
archivesBaseName = 'NoItemDespawn'

def forge_version = "36.0.32"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {

    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.2'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': fileVersion, 'forge_version': forge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {
	exclude('net/**') 
    manifest {
        attributes([
            "Specification-Title": "NoItemDespawn",
            "Specification-Vendor": "Jomcraft Network",
            "Specification-Version": fileVersion,
            "Implementation-Title": "NoItemDespawn",
            "Implementation-Version": fileVersion,
            "Implementation-Vendor" : "Jomcraft Network",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar) {
	exclude('net/**') 
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	exclude('net/**') 
	from sourceSets.main.output
	classifier = 'deobf'
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
}

jar.finalizedBy('reobfJar')

curseforge {

  if(project.hasProperty("cursegradle.api_token") && project.property("cursegradle.api_token") != null && !project.property("cursegradle.api_token").equals("")) {
  	apiKey = project.property("cursegradle.api_token")
  	project {
    	id = '285764'
    	releaseType = 'release'
		addGameVersion '1.16.5'
		addGameVersion '1.16.4'
		addGameVersion '1.16.3'
		addGameVersion '1.16.2'
    	addGameVersion '1.16.1'

    	mainArtifact(jar) {
    		displayName = "$archivesBaseName-$version"
    	}
    
    	addArtifact (sourcesJar) {
    		releaseType = 'release'
    		displayName = "$archivesBaseName-$version-sources"
    	}
    
    	addArtifact (deobfJar) {
    		releaseType = 'release'
    		displayName = "$archivesBaseName-$version-deobf"
    	}
    
  	}
  }
}